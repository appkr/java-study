plugins {
	id 'java'
	id 'idea'
	id 'com.avast.gradle.docker-compose' version '0.16.8'
	id 'org.springframework.boot' version '3.0.1' apply false
	id 'io.spring.dependency-management' version '1.1.0' apply false
	id 'io.freefair.lombok' version '6.6.1' apply false
}

allprojects {
	apply plugin: 'java'
	apply plugin: 'idea'

	group = 'dev.appkr'
	version = '0.0.1-SNAPSHOT'
	sourceCompatibility = '17'
	targetCompatibility = '17'

	repositories {
		mavenCentral()
	}
}

subprojects {
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'io.freefair.lombok'

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-web'
		implementation 'org.springframework.boot:spring-boot-starter-aop'       // To create observations by using the @Observed aspect
		implementation 'org.springframework.boot:spring-boot-starter-actuator'  // To add observation features
		implementation 'io.micrometer:micrometer-registry-prometheus'  			// For Micrometer metrics with Prometheus
		implementation 'com.github.loki4j:loki-logback-appender:1.3.2'			// To publish application logs to Loki
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
	}

	test {
		useJUnitPlatform()
	}
}

dockerCompose {
	useComposeFiles = ['docker/compose.yaml']
	tcpPortsToIgnoreWhenWaiting = [14268]
}


