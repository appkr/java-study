buildscript {
	ext {
		protobufVersion = '3.19.1'
		grpcVersion = '1.42.1'
		grpcSpringVersion = '2.13.1.RELEASE'
	}
}

plugins {
	id 'idea'
	id 'java'
	id 'java-library'
	id 'org.springframework.boot' version '2.5.8'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'io.freefair.lombok' version '6.3.0'
	id 'com.google.protobuf' version '0.8.18'
}

group = 'dev.appkr'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

dependencies {
	implementation platform('org.springframework.cloud:spring-cloud-dependencies:2020.0.5')

	implementation 'org.springframework.boot:spring-boot-starter'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.cloud:spring-cloud-starter-sleuth'
	implementation 'io.zipkin.brave:brave-instrumentation-grpc'

	// grpc
	implementation "io.grpc:grpc-protobuf:${grpcVersion}"
	implementation "io.grpc:grpc-stub:${grpcVersion}"
	compileOnly 'jakarta.annotation:jakarta.annotation-api:1.3.5' // Java 9+ compatibility - Do NOT update to 2.0.0
	implementation "com.google.protobuf:protobuf-java:${protobufVersion}" // Workaround for "cannot find symbol GeneratedMessageV3.isStringEmpty"

	// grpc-spring framework
	implementation "net.devh:grpc-spring-boot-starter:${grpcSpringVersion}"
}

tasks.named('test') {
	useJUnitPlatform()
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:${protobufVersion}"
	}
	generatedFilesBaseDir = "$projectDir/src/generated"
	clean {
		delete generatedFilesBaseDir
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
		}
	}
	generateProtoTasks {
		all()*.plugins {
			grpc {}
		}
	}
}

idea {
	module {
		sourceDirs += file("src/generated/main/java")
		sourceDirs += file("src/generated/main/grpc")
		generatedSourceDirs += file("src/generated/main/java")
		generatedSourceDirs += file("src/generated/main/grpc")
	}
}
