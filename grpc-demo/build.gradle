buildscript {
	ext {
		// Spring version must match with protobuf and grpc
		// see https://yidongnan.github.io/grpc-spring-boot-starter/en/versions.html
		springBootVersion = '2.6.13'
		springDependencyManagementVersion = '1.0.15.RELEASE'
		springCloudVersion = '2021.0.5'
		protobufGradlePluginVersion = '0.9.2'
		protobufVersion = '3.21.12'
		grpcVersion = '1.51.0'
		grpcSpringVersion = '2.14.0.RELEASE'
	}
}

plugins {
	id 'idea'
	id 'java'
	id 'com.avast.gradle.docker-compose' version '0.16.8'
	id 'org.springframework.boot' version "${springBootVersion}" apply false
	id 'io.spring.dependency-management' version "${springDependencyManagementVersion}" apply false
	id 'com.google.protobuf' version "${protobufGradlePluginVersion}" apply false
	id 'io.freefair.lombok' version '6.3.0' apply false
	id 'com.google.cloud.tools.jib' version '3.1.4' apply false
}

allprojects {
	version = '0.0.1-SNAPSHOT'
	group = 'dev.appkr'
	sourceCompatibility = '11'

	repositories {
		mavenCentral()
	}
}

def dockerComposePath = System.getProperty('os.arch').equals('aarch64')
		? 'docker/compose-arm.yml' : 'docker/compose-intel.yml'
dockerCompose {
	useComposeFiles = [dockerComposePath]
}
